trylon-class Rectangle

fields left top right bottom


fn create: left top: top right: right bottom: bottom
	this left = left
	this top = top
	this right = right
	this bottom = bottom

fn create: tuple
	this left = tuple at: 0
	this top = tuple at: 1
	this right = tuple at: 2
	this bottom = tuple at: 3

fn copy
	return Rectangle new: left top: top right: right bottom: bottom


fn width
	return right - left
fn height
	return bottom - top

fn inset-by-h: h v: v
	left += h
	right -= h
	top += v
	bottom -= v

fn offset-by-h: h v: v
	left += h
	right += h
	top += v
	bottom += v


fn top-left
	return Point new: left y: top
fn top-right
	return Point new: right y: top
fn bottom-left
	return Point new: left y: bottom
fn bottom-right
	return Point new: right y: bottom


fn contains: what -> Bool
	if what is-a: Rectangle
		if what left < left || what right > right
			return false
		if what top < top || what bottom > bottom
			return false
		return true

	else if what is-a: Point
		if what x < left || what x > right
			return false
		if what y < top || what y > bottom
			return false
		return true
	
	return false


fn string
	return 	--
		"(" + left string + ", " + top string + ", " + 	--
		right string + ", " + bottom string + ")"


# Copyright 2005-2007 Steve Folta
# See the License file.

