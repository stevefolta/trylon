trylon InstanceFieldSetter
superclass TrylonInstanceFunction

fields field on-class


create: field on-class: on-class
	this field = field
	this on-class = on-class


emit-object-call: object arguments: arguments builder: builder
	# This might not actually be used; there was a syntax error in the generated 
	# code in the Cleet compiler that was never noticed.
	line = 	--
		"FieldOf_(" + object access + ", " + 	--
		(mangle-name: field name) + " = " + 	--
		arguments first-item access + ";"
	builder add-line: line
	return arguments first-item


emit-declaration: stream
	stream write: "extern "
	stream write: c-signature
	stream write-line: ";"


emit-code: stream
	stream write-line: c-signature
	stream write-line: "{"

	stream write: "\tField_("
	stream write: (mangle-name: field name)
	stream write-line: ") = set_to;"
	stream write-line: "\treturn set_to;"

	stream write-line: "}"
	stream write-line
	stream write-line


c-name
	return (mangle-name: field name) + "_co___" + on-class c-name


c-signature
	return "obj_ " + c-name + "(obj_ this_, obj_ set_to)"



