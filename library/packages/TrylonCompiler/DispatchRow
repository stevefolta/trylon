trylon DispatchRow

fields name entries num-entries selector


create: name
	this name = name
	this entries = List new
	this num-entries = 0


append: class-num function: function
	entries append: (DispatchRowEntry new: class-num function: function)
	num-entries += 1


fit-into: dispatch-table used-offsets: used-offsets
	# Find the offset.
	# Very slow method.
	offset = 0
	loop
		if used-offsets at: offset
			offset += 1
			continue

		fits-here = true
		for entry in entries
			if (dispatch-table at: offset + entry class-num) != nil
				fits-here = false
				break
		if fits-here
			break

		offset += 1
	
	# Install the entries.
	selector = offset
	for entry in entries
		(dispatch-table at: offset + entry class-num) = 	--
			DispatchTableEntry new: entry function selector: offset
	(used-offsets at: offset) = true



