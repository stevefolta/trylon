trylon VT100StatusReporter
superclass StatusReporter

fields indent-level last-displayed-indent-level


create
	indent-level = 0
	last-displayed-indent-level = -1


report: message
	# Go back up in the display.
	num-lines-up = last-displayed-indent-level - indent-level + 1
	while num-lines-up > 0
		back-up-one-line
		num-lines-up -= 1
	
	# Indent.
	adjusted-indent-level = indent-level
	if adjusted-indent-level > 20
		adjusted-indent-level = 20
	print: ("                    " substr: 0 length: adjusted-indent-level)

	# Write the message.
	print-line: (message substr: 0 length: 80 - adjusted-indent-level)
	
	last-displayed-indent-level = indent-level


indent
	indent-level += 1


unindent
	indent-level -= 1

	num-lines-up = last-displayed-indent-level - indent-level
	while num-lines-up > 0
		back-up-one-line
		num-lines-up -= 1
		last-displayed-indent-level -= 1


back-up-one-line
	# Go up a line.
	print: "\033[A"

	# Go to the beginning of the line.
	print: "\015"

	# Erase the line.
	print: "\033[2K"


