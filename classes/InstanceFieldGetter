Class: InstanceFieldGetter
Superclass: VlangeInstanceFunction


field: CompiledField;
on-class: CompiledClass;


create(field: CompiledField, on-class: CompiledClass)
{
	this.field = field;
	this.on-class = on-class;
}


emit-object-call(object: ExpressionResult,
                 arguments: (List of ExpressionResult),
                 builder: MethodBuilder)
	: ExpressionResult
{
	return new InstanceFieldResult(object, field, on-class);
}


emit-declaration(stream: OutputStream)
{
	stream.write("extern ");
	stream.write(c-signature);
	stream.write-line(";");
}


emit-code(stream: OutputStream)
{
	stream.write-line(c-signature);
	stream.write-line("{");

	stream.write("\treturn this_->fields[");
	stream.write(MethodBuilder.mangle-name(field.name));
	stream.write-line("__fld_];");

	stream.write-line("}");
	stream.write-line();
	stream.write-line();
}


c-name: String
{
	return MethodBuilder.mangle-name(field.name) + "__" + on-class.c-name;
}


c-signature: String
{
	return "obj_ " + c-name + "(obj_ this_)";
}


