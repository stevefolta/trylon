Class: TryStatement
Superclass: Statement


body: Block;
catch-clauses: List of CatchClause;
post-clauses-comments: List of Statement;


create(body: Block, catch-clauses: (List of CatchClause), comment: String,
       post-clauses-comments: List of Statement)
{
	this.body = body;
	this.catch-clauses = catch-clauses;
	this.comment = comment;
	this.post-clauses-comments = post-clauses-comments;
}


prepare-to-emit()
{
	body.prepare-to-emit();
	for (clause: CatchClause in catch-clauses)
		clause.body.prepare-to-emit();
}


emit-code(builder: MethodBuilder)
{
	builder.using-object-function("is-a:");

	// Body
	builder.add-line(add-comment("_Try_"));
	body.emit-code(builder);

	// Catch clauses
	have-a-catch: Bool = false;
	for (clause: CatchClause in catch-clauses) {
		builder.using-class(clause.type);

		if (clause.pre-clause-comments) {
			for (statement: Statement in clause.pre-clause-comments)
				statement.emit-code(builder);
			}

		if (have-a-catch) {
			builder.add-line(
				add-comment("_AlsoCatch_(" + clause.type.c-name + ")", clause.comment));
			}
		else {
			builder.add-line(
				add-comment("_Catch_(" + clause.type.c-name + ")", clause.comment));
			have-a-catch = true;
			}

		clause.body.emit-code(builder);
		}

	// Finish
	builder.add-line("_EndCatch_");
	if (post-clauses-comments) {
		for (statement: Statement in post-clauses-comments)
			statement.emit-code(builder);
		}
}


