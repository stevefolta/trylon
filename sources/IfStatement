trylon IfStatement
superclass Statement

fields condition body else-block else-comment pre-else-comments


create: condition body: body else-block: else-block 	--
		comment: comment else-comment: else-comment 	--
		pre-else-comments: pre-else-comments
	this condition = condition
	this body = body
	this else-block = else-block
	this comment = comment
	this else-comment = else-comment
	this pre-else-comments = pre-else-comments

create: condition body: body comment: comment
	this condition = condition
	this body = body
	this comment = comment


attach-else: statement line: line
	this else-block = statement


prepare-to-emit
	condition prepare-to-emit
	body prepare-to-emit
	if else-block
		else-block prepare-to-emit


jolt-expression
	else-expression = nil
	if else-block
		size = 2
		if pre-else-comments
			size += pre-else-comments count
		if else-comment
			size += 1
		else-expression = Expression new: size
		else-expression at: 0 put: 'begin'
		index = 1
		if pre-else-comments
			for statement in pre-else-comments
				else-expression at: index put: statement jolt-expression
				index += 1
		if else-comment
			else-expression at: index put: 	--
				(Expression with: 'comment' with: else-comment)
			index += 1
		else-expression at: index put: else-block jolt-expression
	else
		else-expression = 'nil'

	return 	--
		Expression 	--
			with: 'if' 	--
			with: condition jolt-expression 	--
			with: body jolt-expression 	--
			with: else-expression


