trylon Functions

extend Compiler TrylidFunction
	emit-c-call: arguments builder: builder
		virtual


extend Compiler SetArgumentFunction
	emit-c-call: arguments builder: builder
		mangled-name = mangle-name: name
		value = arguments first-item emit-code: builder
		builder add-line: mangled-name + " = " + value access + ";"
		return value


extend Compiler ThisFunction
	emit-c-call: arguments builder: builder
		return CCompiler NameResult new: "this_"


extend Compiler SharedFieldGetter
	emit-c-call: arguments builder: builder
		builder using-shared-field: field on-proto: on-proto
		return Result new: field on-proto: on-proto
	
	class Result
		superclass CCompiler ExpressionResult

		fields field on-proto

		create: field on-proto: on-proto
			this field = field
			this on-proto = on-proto

		access
			return 	--
				"SharedField_(" + 	--
				(mangle-name: field name) + ", " + on-proto c-name + ")"


extend Compiler SharedFieldSetter
	emit-c-call: arguments builder: builder
		builder using-shared-field: field on-proto: on-proto
		value = arguments first emit-code: builder
		builder add-line: 	--
			"SetSharedField_(" + 	--
			(mangle-name: field name) + ", " + on-proto c-name + ", " + 	--
			value + ");"
		return value



