trylid CommandLine
fields prompt

create
	prompt = "> "
	coke
		(using_history)
		(stifle_history 1000)
		(read_history ".trylid-history")
	# I tried using atexit_:, but it crashes; but only if there was a
	# ".trylid-history" already; weird.
	# 	[[Posix Stdlib] atexit_: (lambda () (write_history ".trylid-history"))]


readline
	_prompt = prompt _stringValue
	coke
		(let ((result (readline _prompt)))
			(if result
				(let ((return-value [StandardString new-copy_: result]))
					(add_history result)
					(free result)
					return-value)
				nil))


run
	context = Context new: this
	loop
		line = readline
		if line == nil
			break
		try
			parser = Compiler Parser new: line context: context
			expr = parser parse-expression
			if expr
				result = expr _eval
				print-line: result string
		else
			print-line: "\e[31m" + exception message + "\e[0m"
	print-line
	coke
		(write_history ".trylid-history")


proto Context
	# Ought to work:
	# superclass Compiler Context
	fields command-line parent

	create: command-line
		this command-line = command-line
		parent = Compiler ExistingProto new: "command-line" proto: command-line
	
	lookup-function: name
		return parent lookup-function: name

	lookup-function-autodeclaring: name
		result = lookup-function: name
		if result
			return result

		if (name index-of: `:`) == name length - 1
			# Autodeclare it as a shared field in the command-line.
			field-name = name substr: 0 length: name length - 1
			field-name-symbol = field-name intern
			coke
				(add-shared-field-to field-name-symbol [self command-line] nil)
			field = Compiler CompiledField new: field-name
			return Compiler SharedFieldSetter new: field on-proto: parent

		return nil
	
	enclosing-method-context
		return nil




