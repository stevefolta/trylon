trylon TrylidCodeParser

fields lines context
fields else-target
fields cur-line cur-context

special-forms = nil


create: lines in: context
	if special-forms == nil
		initialize
	this lines = lines
	this context = context
	cur-context = context


initialize
	special-forms = Dictionary new
	special-forms at: "if" put: IfParser new
	special-forms at: "else" put: ElseParser new
	special-forms at: "loop" put: LoopParser new
	special-forms at: "while" put: WhileParser new
	special-forms at: "for" put: ForParser new
	special-forms at: "continue" put: ContinueParser new
	special-forms at: "break" put: BreakParser new
	special-forms at: "return" put: ReturnParser new
	special-forms at: "switch" put: SwitchParser new
	special-forms at: "try" put: TryParser new
	special-forms at: "catch" put: CatchParser new
	special-forms at: "throw" put: ThrowParser new
	special-forms at: "#" put: CommentParser new
	special-forms at: "iff" put: IffParser new


parse
	return parse-block: lines


parse-block: lines
	block = Block new: cur-context
	old-context = cur-context
	cur-context = block
	for line in lines
		cur-line = line
		statement = parse-line: line
		if statement
			if !statement ignored-for-else
				else-target = statement
			block append: statement
	cur-context = old-context
	return block


parse-line: line
	words = line words

	# Is it a special form?
	syntax = special-forms at: words current
	if syntax
		words next
		return syntax parse: words line: line parser: this
	
	# Otherwise, it's an expression.
	# It could be an assignment with a block on the RHS, so we give the block too.
	expression = (TrylidExpressionParser new: words block: line block) parse
	comment = get-comment: words
	return ExpressionStatement new: expression comment: comment


attach-else: statement line: line
	if else-target == nil
		throw ParseException new: "Illegal \"else\"." in: line
	else-target attach-else: statement line: line
	else-target = nil 	# Because you can't have two elses.


get-comment: words
	if words is-empty
		return nil
	if words next == "#"
		return words remainder
	throw ParseException new: "Extra words at end of line." in: cur-line


parse-expression: words
	expression = (TrylidExpressionParser new: words block: cur-line block) parse
	if expression == nil
		error: "Missing expression."
	return expression

parse-optional-expression: words
	return (TrylidExpressionParser new: words block: cur-line block) parse


push-context: context
	cur-context = context
pop-context: context
	cur-context = context parent


error: message
	throw ParseException new: message in: cur-line



# ===== Special Forms

class IfParser
	create

	parse: words line: line parser: parser
		expression = parser parse-expression: words
		comment = parser get-comment: words
		body = parser parse-block: line block
		return IfStatement new: expression body: body comment: comment


class ElseParser
	create

	parse: words line: line parser: parser
		statement = nil
		if words peek == "if"
			words next
			statement = (IfParser new) parse: words line: line parser: parser
		else
			comment = parser get-comment: words
			statement = parser parse-block: line block
			if comment
				# "statement" will be a Block.
				statement prepend: (Comment new: comment)
		parser attach-else: statement line: line
		parser else-target = statement 	# In case it's an "if" statement.
		return nil


class LoopParser
	create

	parse: words line: line parser: parser
		comment = parser get-comment: words
		body = parser parse-block: line block
		return LoopStatement new: body comment: comment


class WhileParser
	create

	parse: words line: line parser: parser
		condition = parser parse-expression: words
		comment = parser get-comment: words
		body = parser parse-block: line block
		return WhileStatement new: condition body: body comment: comment


class ForParser
	create

	parse: words line: line parser: parser
		# Get the name of the local.
		name = words next
		if !is-name: name
			parser error: "Bad name."
		local = CompiledField new: name

		# "in".
		in-token = words next
		if in-token != "in"
			parser error: "Missing \"in\"."

		# Collection, comment.
		collection = parser parse-expression: words
		comment = parser get-comment: words

		# Body.
		context = ForContext new: local parent: parser cur-context
		parser push-context: context
		body = parser parse-block: line block
		parser pop-context: context

		return 	--
			ForStatement new: local collection: collection body: body comment: comment


class BreakParser
	create

	parse: words line: line parser: parser
		return BreakStatement new: (parser get-comment: words)


class ContinueParser
	create

	parse: words line: line parser: parser
		return ContinueStatement new: (parser get-comment: words)


class ReturnParser
	create

	parse: words line: line parser: parser
		value = parser parse-optional-expression: words
		comment = parser get-comment: words
		return ReturnStatement new: value comment: comment


class SwitchParser
	create

	parse: words line: line parser: parser
		#...


class TryParser
	create

	parse: words line: line parser: parser
		#...


class CatchParser
	# For Trylon compatibility.  CatchParser will be a special kind of
	# ElseParser that accepts extra tokens on the line.
	create

	parse: words line: line parser: parser
		#...


class ThrowParser
	create

	parse: words line: line parser: parser
		#...


class CommentParser
	create

	parse: words line: line parser: parser
		#...


class IffParser
	create

	parse: words line: line parser: parser
		#...



