trylon CompiledProto
superclass Context

fields name parent functions fields shared-fields superclass directories
fields uses-protos subclasses
fields source-name 	# The name given in the "trylon" line.
fields is-main is-root-object


create: name parent: parent
	this name = name
	this parent = parent
	functions = Dictionary new
	fields = List new
	shared-fields = Dictionary new
	directories = List new
	uses-protos = List new
	subclasses = List new
	is-main = false
	is-root-object = false

	# Will be added by Jolt code:
	if build-settings targeting-jolt
		add-function: (PrimitiveFunction new: ".proto" on-proto: this)
		add-function: (PrimitiveFunction new: ".superclass" on-proto: this)


lookup-function: name
	return lookup-function: name visited-protos: ObjectMap new


lookup-function: name visited-protos: visited-protos
	# Recursion control.
	if visited-protos contains: this
		return nil
	visited-protos at: this put: this

	# Do we know it already?
	function = functions at: name
	if function
		return function
	
	# If not, see if it's in one of our directories.
	for directory in directories
		# A file?
		if (directory contains: name) && !(directory entry-is-directory: name)
			proto = add-proto: name
			lines = (LinesLexer new: (directory at: name) contents) lex
			TrylonProtoParser parse-proto-contents: lines into: proto
			return functions at: name

		# A directory?
		if directory entry-is-directory: name
			add-proto: name directory: (directory at: name)
			return functions at: name
	
	# Check the parent.
	if parent
		function = parent lookup-function: name visited-protos: visited-protos
		if function
			return function
	
	# Next, check the used protos.
	for used-proto in uses-protos
		function = 	--
			used-proto lookup-function: name visited-protos: visited-protos
		if function
			return function
	
	return nil


lookup-instance-function: name
	function = functions at: name
	if function
		return function
	
	if superclass
		return superclass lookup-instance-function: name
	
	return nil


make-subclass-of: superclass
	if this superclass
		# Already had a superclass (perhaps the default Standard Object); 
		# replace it.
		uses-protos remove: this superclass
	
	# Install the superclass.
	this superclass = superclass
	superclass subclasses append: this


add-function: function
	function-name = function name
	if function has-code
		existing-function = functions at: function-name
		if existing-function && existing-function has-code
			throw MessageException new: 	--
				"\"" + this name + " " + function-name + "\" is defined twice."

	functions at: function-name put: function
	compiler declaring-selector: function-name

	# "create" functions automatically add a corresponding "new:" function.
	if (function is-a: CompiledFunction) && 	--
	    ((function-name == "create") || 	--
			 (function-name starts-with: "create:") || 	--
	     (function-name starts-with: "create-"))
		# Add the corresponding "new" function.
		new-function = NewFunction new: function on-proto: this
		(functions at: new-function name) = new-function
		compiler declaring-selector: new-function name


add-function: name function: function
	functions at: name put: function
	compiler declaring-selector: name


add-field: field
	fields append: field
	add-function: field name 	--
		function: (FieldGetter new: field on-proto: this)
	add-function: field name + ":" 	--
		function: (FieldSetter new: field on-proto: this)


add-shared-field: field
	shared-fields at: field name put: field
	add-function: field name 	--
		function: (SharedFieldGetter new: field on-proto: this)
	add-function: field name + ":" 	--
		function: (SharedFieldSetter new: field on-proto: this)


add-proto: name directory: directory
	# If it already exists, return it.
	existing-function = functions at: name
	if existing-function
		if !(existing-function is-a: ProtoFunction)
			throw MessageException new: 	--
				"Adding \"" + name + "\" as a proto, when it's already a non-proto."
		return existing-function proto
	
	# Add the new one.
	status-reporter = compiler status-reporter
	status-reporter report: "Loading " + name + " into " + full-name + "..."
	status-reporter indent
	new-proto = CompiledProto new: name parent: this
	if directory
		new-proto add-directory: directory
	proto-function = ProtoFunction new: new-proto
	add-function: name function: proto-function
	compiler proto-queue append: new-proto
	status-reporter unindent
	return new-proto


add-proto: name
	return add-proto: name directory: nil


get-subproto: name
	# Has it been loaded already?
	function = functions at: name
	if function && (function is-a: ProtoFunction)
		return function proto
	
	# Look for it.
	for directory in directories
		if directory entry-is-directory: name
			return add-proto: name directory: (directory at: name)
		else if directory contains: name
			proto = add-proto: name
			lines = (LinesLexer new: (directory at: name) contents) lex
			TrylonProtoParser parse-proto-contents: lines into: proto
			return proto
	
	return nil


get-proto: name
	function = functions at: name
	if function == nil
		function = lookup-function: name
	if function && (function is-a: ProtoFunction)
		return function proto
	return nil


add-directory: directory
	directories append: directory

	if directory contains: "main"
		lines = (LinesLexer new: (directory at: "main") contents) lex
		TrylonProtoParser parse-proto-contents: lines into: this


setup-main: main-directory library-directory: library-directory
	is-main = true

	# We want the main-directory to have precedence when looking up names,
	# but we won't be able to compile its "main" until the library directory
	# is in the "directories" and Standard is visible.  So add both, *then*
	# compile the "main"s.
	directories append: main-directory
	directories append: library-directory

	# Load Standard.
	standard-proto = get-subproto: "Standard"
	if standard-proto == nil
		throw MessageException new: "The Standard proto can't be found!"
	uses-protos append: standard-proto

	# Targeting Jolt.
	if build-settings targeting-jolt
		standard-proto get-subproto: "JoltImplementation"

		# Set up Posix.
		posix-directory = library-directory at: "IdPosix"
		posix-proto = 	--
			JoltCompiler PosixProto 	--
				new: "Posix" parent: this directory: posix-directory
		proto-function = ProtoFunction new: posix-proto
		functions at: "Posix" put: proto-function
		compiler proto-queue append: posix-proto

		# Set up Standard Pepsi.
		standard-directory = library-directory at: "Standard"
		pepsi-proto = PepsiProto new: "Pepsi" parent: standard-proto
		proto-function = ProtoFunction new: pepsi-proto
		standard-proto functions at: "Pepsi" put: proto-function
		lines = (LinesLexer new: (standard-directory at: "Pepsi") contents) lex
		TrylonProtoParser parse-proto-contents: lines into: pepsi-proto
		compiler proto-queue append: pepsi-proto
	
	# Targeting C.
	if compiler build-settings targeting-c
		standard-proto get-subproto: "CImplementation"

	# Add "nil", "true", and "false" to Standard.
	standard-proto functions at: "nil" put: NilFunction new
	standard-proto functions at: "true" put: (BoolLiteralFunction new: true)
	standard-proto functions at: "false" put: (BoolLiteralFunction new: false)

	# Load our "main"s
	for directory in directories
		if directory contains: "main"
			lines = (LinesLexer new: (directory at: "main") contents) lex
			TrylonProtoParser parse-proto-contents: lines into: this
	
	# Load up standard protos.
	std-protos = ("BytePtr", "Int", "String", "Float", "Symbol", "Tuple", "List")
	for proto-name in std-protos
		standard-proto lookup-function: proto-name
	
	# Self.
	functions at: name put: (ProtoFunction new: this)


ensure-superclasses: object-proto
	if superclass == nil && object-proto != this
		make-subclass-of: object-proto
	for function in functions values
		if function is-a: ProtoFunction
			proto = function proto
			if proto != this
				proto ensure-superclasses: object-proto


prepare-to-emit
	if superclass == nil
		ensure-superclasses: (compiler get-standard-class: "Object")
	
	for function in functions values
		function prepare-to-emit



# Helpers

full-name
	if is-main
		return name
	result = this name
	proto = parent
	while !proto is-main
		result = proto name + " " + result
		proto = proto parent
	return result



# Copyright 2007 Steve Folta.  See the License file.

