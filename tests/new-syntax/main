trylon-package Main

pre-declared = nil
string-field = "Hello"
symbol-field = 'checkity-test'
int-field = 7
float-field = 3.14
character-field = `x`
true-field = true
false-field = false
nil-field = nil
typed-global (Int) = 5


main: args
	try
		tests
	catch Exception
		print-line
		print-line: exception message


test: succeeded title: title
	if succeeded
		print-line: title + ":  succeeded."
	else
		throw MessageException new: "*** " + title + " test failed. ***"

check: value against: expected-value title: title
	if value == expected-value
		print-line: title + ":  succeeded."
	else
		throw MessageException new: "*** " + title + " test failed. ***"


tests
	# 2005.5.5?:  Autodeclarations.
	x = 3
	check: x against: 3 title: "Autodeclaration of local"
	if true
		pre-declared = true
	test: pre-declared title: "Autodeclaration doesn't hide global"
	check: string-field against: "Hello" title: "String field value"
	check: symbol-field against: 'checkity-test' title: "Symbol field value"
	check: int-field against: 7 title: "Int field value"
	check: float-field against: 3.14 title: "Float field value"
	check: character-field against: `x` title: "Character field value"
	check: true-field against: true title: "True field value"
	check: false-field against: false title: "False field value"
	test: nil-field == nil title: "Nil field value"
	print-line

	# 2006.1.21: "(type)" for type declarations.
	y (Int) = 3
	check: y against: 3 title: "(Type)"
	check: typed-global against: 5 title: "(Type) in global"
	print-line

	# 2006.3.5: "switch" statement.
	switch y
		1
			test: false title: "Switch statement (Int)"
		2
			test: false title: "Switch statement (Int)"
		3
			test: true title: "Switch statement (Int)"
	switch symbol-field 
		'foo'
			test: false title: "Switch statement (Symbol)"
		'bar'
			test: false title: "Switch statement (Symbol)"
		'checkity-test'
			test: true title: "Switch statement (Symbol)"
	switch symbol-field
		'foo'
			test: false title: "Switch statement (else)"
		else
			test: true title: "Switch statement (else)"
	switch "foo"
		"foo", "bar"	# Comment here...
			test: true title: "Switch statement (multiple values)"
		else
			test: true title: "Switch statement (multiple values)"
	print-line
	
	# 2006.3.19: Dictionary literals.
	dict =
		foo = "Foo value"
		bar = "Bar value"
	check: (dict at: "foo") against: "Foo value" title: "Dict literal"
	check: (dict at: "bar") against: "Bar value" title: "Dict literal"


blegh: index (Int)
	return index


class Foo
	fields index (Int)


