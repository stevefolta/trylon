trylon-package Main

class Window
	superclass Carbon WindowManager Window

	fn create
		super create: (Rectangle new: (100, 100, 400, 400)) --
			attributes: ('standard-document-attributes', 'standard-handler', 	--
			             'in-window-menu')

	fn handle-event: event
		print-line: "Got window event."
		if event kind == 'draw-content'
			print-line: "...it's a draw-content event."
			port-rect := this port-bounds
			Carbon QuickDraw erase-rect: port-rect
			context := Carbon QuickDraw begin-cg-context: Carbon QuickDraw port
			draw: context
			Carbon QuickDraw end-cg-context: Carbon QuickDraw port context: context

	fn draw: context
		font-id := Carbon ATSUI find-font: "Georgia"
		if font-id == Carbon ATSUI invalid-font-id
			print-line: "Couldn't find the font!"
		style := Carbon ATSUI Style new
		style font = font-id
		style size = 31
		layout := Carbon ATSUI TextLayout new
		layout text = "Hello world!"
		layout style = style
		layout cg-context = context
		layout draw-x: 20 y: 40

fn main: args
	menu := Carbon MenuManager Menu new: 1 title: "File"
	menu append-item: "Quit"
	Carbon MenuManager insert-menu: menu before: 0
	Carbon MenuManager draw-menu-bar
	window := Window new
	window show
	Carbon EventManager run-application-event-loop

