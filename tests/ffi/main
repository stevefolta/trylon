trylon ffi-test

class PseudoPosix
	# Declaring printf(), etc. in Main results in warnings from the C compiler,
	# so we make this namespace to contain them.

	class printf
		superclass CFunction IntFunction
		call: args
			return call: "printf" with: args

	class strdup
		superclass CFunction StringFunction
		call: args
			return call: "strdup" with: args
	
	class cos
		superclass CFunction DoubleFunction
		call: args
			return call: "cos" with: args
	
	test
		len = printf("Value: %d.\n", 12345)
		check: len against: 14 title: "printf() return value"
		check: strdup("foo") against: "foo" title: "strdup() return value"
		check: cos(0.0) against: 1.0 title: "cos() return value"
		sin = CFunction DoubleFunction new: "sin"
		check: sin(0.0) against: 0.0 title: "sin() return value"
		iff test-builtin-functions
			# MUST have "-rdynamic" in the link-flags for this to work.
			builtin-function = CFunction IntFunction new: "builtin_function"
			check: builtin-function() against: 23456 title: "builtin_function()"


main: args
	try
		PseudoPosix test
	else
		send: exception message
		return 1
	return 0


check: value against: expected-value title: title
	test: value == expected-value title: title

test: succeeded title: title
	if succeeded
		print-line: title + ": succeeded."
	else
		throw MessageException new: "*** " + title + ": FAILED. ***"


c-preamble
	int builtin_function()
	{
		return 23456;
	}


