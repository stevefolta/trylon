trylon-package Main

fn main: args
	print-line: "-- Statement tests --"

	# if
	if-test: 2
	if-test: 3
	if-test: 4
	print-line: "--"

	# loop
	i [Int] := 0
	loop
		i += 1
		if i == 4
			continue
		print-line: i string
		if i == 10
			break
	print-line: "--"

	# while
	i = 1
	while i <= 10
		print-line: i string
		i += 1
	print-line: "--"

	# for
	for i in Interval new: 7 to: 9
		print-line: i string
	print-line: "--"

	# throw/catch
	try
		throw-test
	catch Exception
		print-line: exception message
	iff test-uncaught-exception
		try
			throw-uncaught-test
		catch Exception
			print-line: exception message
	print-line: "--"


	print-line
	print-line: "-- Expression Tests --"

	print-line: (3 + 4) string + " == 7"
	print-line: (3 + 4 * 5) string + " == 23"
	print-line: (3 == 3) string + " == true"
	print-line: (3 == 4) string + " == false"
	x := 3
	print-line: (9 / x) string + " == 3"
	print-line: (10 % x) string + " == 1"
	print-line: "--"
	print-line: (true || 3) string + " == true"
	print-line: (false || 3) string + " == 3"
	print-line: (true && 3) string + " == 3"
	print-line: (false && 3) string + " == false"
	print-line: (!false) string + " == true"
	print-line: "--"
	print-line: (3 & 7) string + " == 3"
	print-line: (3 | 7) string + " == 7"
	print-line: (3 ^ 7) string + " == 4"
	print-line: (~-2) string + " == 1"
	foo = 7
	print-line: foo string + " == 7"
	print-line: "3" float string + " == 3"

	print-line
	print-line: "-- Float Tests --"

	x := 3.2
	print-line: (x + 2.1) string + " == 5.3"
	print-line: (x - 2.1) string + " == 1.1"
	print-line: (x * 2.0) string + " == 6.4"
	print-line: (x / 2.0) string + " == 1.6"
	print-line: (x < 3.0) string + " == false"
	print-line: (x > 3.0) string + " == true"
	print-line: (x == 3.2) string + " == true"
	print-line: (x != 3.2) string + " == true"
	print-line: (3 float + 0.2) string + " == 3.2"
	print-line: 3.2 int string + " == 3"
	print-line: "3.2" float string + " == 3.2"

	print-line
	print-line: "-- Other Tests --"

	test-obj := PrivateClass new
	test-obj go
	PrivatePackage go
	print-line

	list := List new
	list append: "List item 1"
	list append: "List item 2"
	list append: "List item 3"
	for item in list
		print-line: item
	print-line

	tuple := "Tuple item 1", "Tuple item 2", "Tuple item 3"
	for item in tuple
		print-line: item
	print-line

	for item in "Single item"
		print-line: item
	print-line

	if 'foo' == 'foo'
		print-line: "Symbol equality worked."
	if 'foo' != 'bar'
		print-line: "Symbol inequality worked."
	print-line

	abcde := "abcde"
	print-line: (abcde substr: 3) + " == de"
	print-line: (abcde substr: 2 length: 2) + " == cd"

	#
	print-line
	return 0


fn if-test: arg
	if arg == 2
		print-line: "It's two."
	else if arg == 3
		print-line: "It's three."
	else
		print-line: "It's not two or three."

fn throw-test
	throw MessageException new: "Boo!"

fn throw-uncaught-test
	throw 35


field value
fn foo
	return value
fn foo: new-value
	value = new-value


class PrivateClass
	fn create
		#...
	fn go
		print-line: "Private class worked."

package PrivatePackage
	fn go
		print-line: "Private package worked."
